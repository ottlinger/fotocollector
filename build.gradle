import java.text.SimpleDateFormat

plugins {
    id 'net.researchgate.release' version '3.0.2'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS

Date buildTimeAndDate = new Date()
ext {
    // Generate JAR manifest only if code was compiled or recompiled;
    // otherwise the junit5Test task will always be executed even if
    // no code changes were made. The reason is that the generation of
    // the buildDate and buildTime causes JAR manifests to be modified
    // which triggers unnecessary rebuilding of the dependent JARs.
    generateManifest = false
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

apply plugin: 'application'
apply plugin: 'java'


tasks.withType(JavaCompile) {
    sourceCompatibility = rootProject.sourceCompatibility
    targetCompatibility = rootProject.targetCompatibility
}
// Codecov integration
apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.10"
}
jacocoTestReport {
    reports {
        // v8: xml.enabled = true
        // v8: html.enabled = false
    }
}

apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'clean', 'build'

mainClassName = 'de.aikiit.fotocollector.FotoCollectorApplication'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.1'
    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testImplementation group: 'junit', name: 'junit', version: '4.+'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

jar {
    onlyIf {
        project.generateManifest
    }
    manifest {
        attributes(
                'Implementation-Title': 'FotoCollector',
                'Implementation-Version': project.version,
                'Main-Class': mainClassName,
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                'Build-Date': buildDate,
                'Build-Time': buildTime,
                'Specification-Title': project.name,
                'Specification-Version': project.version
        )
    }
}

check.dependsOn jacocoTestReport
