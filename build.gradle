import java.text.SimpleDateFormat

plugins {
    id 'net.researchgate.release' version '3.0.2'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'jacoco'
    id 'project-report'
}

gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS

Date buildTimeAndDate = new Date()
ext {
    // Generate JAR manifest only if code was compiled or recompiled;
    // otherwise the junit5Test task will always be executed even if
    // no code changes were made. The reason is that the generation of
    // the buildDate and buildTime causes JAR manifests to be modified
    // which triggers unnecessary rebuilding of the dependent JARs.
    generateManifest = false
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

apply plugin: 'application'
apply plugin: 'java'

// set in gradle.properties
tasks.withType(JavaCompile) {
    sourceCompatibility = rootProject.sourceCompatibility
    targetCompatibility = rootProject.targetCompatibility
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        html.required = false
    }
}

jacoco {
    toolVersion = "0.8.12"
    // reportsDirectory = layout.buildDirectory.dir('reports/jacoco/test/html/')
}

apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'clean', 'build'

mainClassName = 'de.aikiit.fotocollector.FotoCollectorApplication'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'com.google.guava:guava:33.3.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    implementation 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:slf4j-log4j12:2.0.16'
}

jar {
    onlyIf {
        project.generateManifest
    }
    manifest {
        attributes(
                'Implementation-Title': 'FotoCollector',
                'Implementation-Version': project.version,
                'Main-Class': mainClassName,
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                'Build-Date': buildDate,
                'Build-Time': buildTime,
                'Specification-Title': project.name,
                'Specification-Version': project.version
        )
    }
}

check.dependsOn jacocoTestReport

test {
    useJUnitPlatform()
}