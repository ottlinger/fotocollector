import java.text.SimpleDateFormat

plugins {
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
}

gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS

Date buildTimeAndDate = new Date()
ext {
    // Generate JAR manifest only if code was compiled or recompiled;
    // otherwise the junit5Test task will always be executed even if
    // no code changes were made. The reason is that the generation of
    // the buildDate and buildTime causes JAR manifests to be modified
    // which triggers unnecessary rebuilding of the dependent JARs.
    generateManifest = false
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

apply plugin: 'application'
apply plugin: 'java'


tasks.withType(JavaCompile) {
    sourceCompatibility = rootProject.sourceCompatibility
    targetCompatibility = rootProject.targetCompatibility
}
// Codecov integration
apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.4"
}
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'clean', 'build'

mainClassName = 'de.aikiit.fotocollector.FotoCollectorApplication'

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-codec:commons-codec:1.12'
    compile 'com.google.guava:guava:28.0-jre'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile 'org.mockito:mockito-core:2.28.2'
    testCompile 'org.assertj:assertj-core:3.12.2'
}

jar {
    onlyIf {
        project.generateManifest
    }
    manifest {
        attributes(
                'Implementation-Title': 'FotoCollector',
                'Implementation-Version': version,
                'Main-Class': mainClassName,
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                'Build-Date': buildDate,
                'Build-Time': buildTime,
                'Specification-Title': project.name,
                'Specification-Version': project.version
        )
    }
}

check.dependsOn jacocoTestReport
